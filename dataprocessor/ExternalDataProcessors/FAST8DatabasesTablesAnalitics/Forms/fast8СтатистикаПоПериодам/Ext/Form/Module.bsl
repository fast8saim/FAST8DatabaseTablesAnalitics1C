// MIT License
//
// Copyright (c) 2023 FAST8.RU
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ПолноеИмяМетаданных", fast8ПолноеИмяМетаданных);
	
	Если Не ЗначениеЗаполнено(fast8ПолноеИмяМетаданных) Тогда
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
	Иначе
		ЗаполнитьГрафик();
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти // ОбработчикиСобытийФормы

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьГрафик()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если СтрокаНачинаетсяС(fast8ПолноеИмяМетаданных, "Документ") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РасчетКоличества.Месяц КАК Месяц,
		|	СУММА(РасчетКоличества.Проведенные) КАК Проведенные,
		|	СУММА(РасчетКоличества.НеПроведенные) КАК НеПроведенные,
		|	СУММА(РасчетКоличества.ПомеченныеНаУдаление) КАК ПомеченныеНаУдаление
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДАТАВРЕМЯ(2020, 1, 1) КАК Месяц,
		|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(30, 0)) КАК Проведенные,
		|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(30, 0)) КАК НеПроведенные,
		|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(30, 0)) КАК ПомеченныеНаУдаление
		|	ГДЕ
		|		ЛОЖЬ // КОМАНДА СУБД УВЕЛИЧИТЬ РАЗМЕРНОСТЬ ЧИСЕЛ
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НАЧАЛОПЕРИОДА(Таблица.Дата, МЕСЯЦ),
		|		ВЫБОР
		|			КОГДА Таблица.Проведен
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА НЕ Таблица.ПометкаУдаления
		|					И НЕ Таблица.Проведен
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА Таблица.ПометкаУдаления
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	ИЗ
		|		" + fast8ПолноеИмяМетаданных + " КАК Таблица) КАК РасчетКоличества
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетКоличества.Месяц
		|
		|УПОРЯДОЧИТЬ ПО
		|	Месяц";
		
		СерияПроведенные			= fast8График.Серии.Добавить("Проведенные");
		СерияНеПроведенные			= fast8График.Серии.Добавить("Не проведенные");
		СерияПомеченныеНаУдаление	= fast8График.Серии.Добавить("Помеченные на удаление");
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяТочка = fast8График.Точки.Добавить(Формат(Выборка.Месяц, "ДФ='ММММ гггг'"));
			fast8График.УстановитьЗначение(НоваяТочка, СерияПроведенные,			Выборка.Проведенные);
			fast8График.УстановитьЗначение(НоваяТочка, СерияНеПроведенные,			Выборка.НеПроведенные);
			fast8График.УстановитьЗначение(НоваяТочка, СерияПомеченныеНаУдаление,	Выборка.ПомеченныеНаУдаление);
		КонецЦикла;
		
	ИначеЕсли СтрокаНачинаетсяС(fast8ПолноеИмяМетаданных, "Регистр") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РасчетКоличества.Месяц КАК Месяц,
		|	СУММА(РасчетКоличества.Количество) КАК Количество
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДАТАВРЕМЯ(2020, 1, 1) КАК Месяц,
		|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(30, 0)) КАК Количество
		|	ГДЕ
		|		ЛОЖЬ // КОМАНДА СУБД УВЕЛИЧИТЬ РАЗМЕРНОСТЬ ЧИСЕЛ
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ),
		|		1
		|	ИЗ
		|		" + fast8ПолноеИмяМетаданных + " КАК Таблица) КАК РасчетКоличества
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетКоличества.Месяц
		|
		|УПОРЯДОЧИТЬ ПО
		|	РасчетКоличества.Месяц";
		
		Если СтрокаНачинаетсяС(fast8ПолноеИмяМетаданных, "РегистрРасчета") Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, ".Период", ".ПериодРегистрации");
		КонецЕсли;
		
		СерияКоличество	= fast8График.Серии.Добавить("Количество");
				
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяТочка = fast8График.Точки.Добавить(Формат(Выборка.Месяц, "ДФ='ММММ гггг'"));
			fast8График.УстановитьЗначение(НоваяТочка, СерияКоличество, Выборка.Количество);
		КонецЦикла;
		
	ИначеЕсли СтрокаНачинаетсяС(fast8ПолноеИмяМетаданных, "БизнесПроцесс") Или СтрокаНачинаетсяС(fast8ПолноеИмяМетаданных, "Задача") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РасчетКоличества.Месяц КАК Месяц,
		|	СУММА(РасчетКоличества.Количество) КАК Количество,
		|	СУММА(РасчетКоличества.ПомеченныеНаУдаление) КАК ПомеченныеНаУдаление
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДАТАВРЕМЯ(2020, 1, 1) КАК Месяц,
		|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(30, 0)) КАК Количество,
		|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(30, 0)) КАК ПомеченныеНаУдаление
		|	ГДЕ
		|		ЛОЖЬ // КОМАНДА СУБД УВЕЛИЧИТЬ РАЗМЕРНОСТЬ ЧИСЕЛ
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НАЧАЛОПЕРИОДА(Таблица.Дата, МЕСЯЦ),
		|		ВЫБОР
		|			КОГДА НЕ Таблица.ПометкаУдаления
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА Таблица.ПометкаУдаления
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	ИЗ
		|		" + fast8ПолноеИмяМетаданных + " КАК Таблица) КАК РасчетКоличества
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетКоличества.Месяц
		|
		|УПОРЯДОЧИТЬ ПО
		|	Месяц";
		
		СерияКоличество				= fast8График.Серии.Добавить("Количество");
		СерияПомеченныеНаУдаление	= fast8График.Серии.Добавить("Помеченные на удаление");
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяТочка = fast8График.Точки.Добавить(Формат(Выборка.Месяц, "ДФ='ММММ гггг'"));
			fast8График.УстановитьЗначение(НоваяТочка, СерияКоличество,				Выборка.Количество);
			fast8График.УстановитьЗначение(НоваяТочка, СерияПомеченныеНаУдаление,	Выборка.ПомеченныеНаУдаление);
		КонецЦикла;	
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьГрафик()

&НаСервере
Функция СтрокаНачинаетсяС(ИсходнаяСтрока, Вхождение)
	
	// СтрНачинаетсяС
	
	Возврат (Лев(ИсходнаяСтрока, СтрДлина(Вхождение)) = Вхождение);
	
КонецФункции // СтрокаНачинаетсяС()

#КонецОбласти // СлужебныеПроцедурыИФункции